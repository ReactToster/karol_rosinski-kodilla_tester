plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id "com.github.spotbugs" version "4.7.2"
    id 'org.springframework.boot' version '2.5.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group = 'com.kodilla'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    runtimeOnly 'mysql:mysql-connector-java'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'METHOD'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
            includes = [
                    'com.kodilla.jacoco.*'
            ]
        }
    }
}

checkstyle {
    toolVersion "8.30"
    configFile = file("checkstyle.xml")
}

spotbugs {
    ignoreFailures = true
}

spotbugsMain {
    reports {
        html {
            required = true
        }
    }
}

spotbugsTest {
    reports {
        html {
            required = true
        }
    }
}

test {
    useJUnitPlatform()
}